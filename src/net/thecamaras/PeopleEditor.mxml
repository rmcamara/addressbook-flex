<?xml version="1.0" encoding="utf-8"?>
<thecamaras:CommonEditor
    xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns:thecamaras="net.thecamaras.*"
    icon="{PERSON_ICON}" preinitialize="establishName()">
    
    <mx:Style >        
        .personTitle {
            font-size:30;
            text-align:center;
        }
    </mx:Style>

    <mx:Script >
        <![CDATA[
            import net.thecamaras.models.BaseModel;
            import mx.messaging.messages.IMessage;
            import mx.rpc.events.FaultEvent;
            import mx.events.CloseEvent;
            import mx.utils.ObjectUtil;
            import net.thecamaras.models.Person;
            import mx.controls.Alert;
            
            [Embed(source="assets/person.png")]
            private static const PERSON_ICON:Class;
            
            [Bindable]
            private var person:Person;
            
            override protected function createChildren():void{
                super.createChildren();
                
                var pEditor:PersonEditor;
                pEditor = new PersonEditor();
                busyContainer.addChildAt(pEditor, 0);
                pEditor.width = Number("50%");
                
                editor = pEditor;
            }
            
            private function establishName():void {
                titleText = "New Person";
            }

            override public function setId (id:int):void{
                super.setId(id);
                var args:Object = new Object();
                args.method = "Person";
                args.id = id;
                Connection.instance.send("address.php", args, loadModel); 
            }
            
            /**
             * Handles the loading of the Person model from the xml returned by the service call.
             * 
             * @param xml The xml results of the service request. 
             */  
            override protected function loadModel(xml:XML):void{
                this.person = new Person(xml.data.Person[0]);
                this.label = person.firstname;
                titleText = person.firstname + ' ' + person.lastname;
                PersonEditor(this.editor).person = this.person;
                busyContainer.enabled = true;
                
                // Display the current personel details
                displayBox.removeAllChildren();
                PersonRenderer(displayBox.addChild(new PersonRenderer())).source = xml.data.Person[0];
                displayBox.getChildAt(0).height = 80;
                
                // establish viewer the possible locations
                var pViewer:PlaceRenderer;                
                this.childListing.removeAllChildren();
                for each (var place:XML in xml.data.Person.Places.Place){
                    pViewer = new PlaceRenderer();
                    
                    this.childListing.addChild(pViewer);
                    pViewer.source = place;
                }
            }
            
            /**
             * User has requested a save to happen. Reads out all the values from the person
             * editor and sends them to the service call. 
             */  
            override protected function buildSaveRequest():Object{
                var pEditor:PersonEditor = PersonEditor(editor);
                
                var args:Object = new Object();
                args.method = "SavePerson";
                args.id = person.id;
                args.title = pEditor.title.text;
                args.firstname = pEditor.firstName.text;
                args.lastname = pEditor.lastName.text;
                args.email = pEditor.email.text;
                args.cell = pEditor.cellphone.text;
                // Reformat the date to one that php/mysql can understand
                args.birth = BaseModel.DATE_FORMATER.format(pEditor.birth.text);
                args.details = pEditor.details.text;
                return args;
            }
            
        ]]>
    </mx:Script>
</thecamaras:CommonEditor>
