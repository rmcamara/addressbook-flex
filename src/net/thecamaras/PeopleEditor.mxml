<?xml version="1.0" encoding="utf-8"?>
<!--
///////////////////////////////////////////////////////////////////////////////
//    
//    Copyright 2008 Ross Camara
//
//    This file is part of Addressbook.
//
//    Foobar is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    AddressBook is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
//
///////////////////////////////////////////////////////////////////////////////
-->
<thecamaras:CommonEditor
    xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns:thecamaras="net.thecamaras.*"
    icon="{PERSON_ICON}" preinitialize="establishLabels()">
    
    <mx:Style >        
        .personTitle {
            font-size:30;
            text-align:center;
        }
    </mx:Style>

    <mx:Script >
        <![CDATA[
            import mx.containers.TitleWindow;
            import net.thecamaras.models.BaseModel;
            import mx.messaging.messages.IMessage;
            import mx.rpc.events.FaultEvent;
            import mx.events.CloseEvent;
            import mx.utils.ObjectUtil;
            import net.thecamaras.models.Person;
            import mx.controls.Alert;
            
            [Embed(source="assets/person.png")]
            private static const PERSON_ICON:Class;
            
            [Bindable]
            private var person:Person;
            
            override protected function createChildren():void{
                super.createChildren();
                
                var pEditor:PersonEditor;
                pEditor = new PersonEditor();
                busyContainer.addChildAt(pEditor, 0);
                pEditor.width = Number("50%");
                
                editor = pEditor;
            }
            
            private function establishLabels():void {
                titleText = "New Person";
                childText = "Possible Addresses";
            }

            override public function init (id:int):void{
                super.init(id);
                var args:Object = new Object();
                args.method = "Person";
                args.id = id;
                Connection.instance.send("address.php", args, loadModel); 
            }
            
            /**
             * Handles the loading of the Person model from the xml returned by the service call.
             * 
             * @param xml The xml results of the service request. 
             */  
            override protected function loadModel(xml:XML):void{
                super.loadModel(xml);
                
                this.person = new Person(xml.data.Person[0]);
                this.label = person.firstname;
                titleText = person.firstname + ' ' + person.lastname;
                PersonEditor(this.editor).person = this.person;
                
                
                // Display the current personel details
                displayBox.removeAllChildren();
                PersonRenderer(displayBox.addChild(new PersonRenderer())).source = xml.data.Person[0];
                
                // establish viewer the possible locations
                var pViewer:PlaceRenderer;                
                this.childListing.removeAllChildren();
                for each (var place:XML in xml.data.Person.Places.Place){
                    pViewer = new PlaceRenderer();
                    
                    this.childListing.addChild(pViewer);
                    pViewer.source = place;
                }
            }
            
            /**
             * User has requested a save to happen. Reads out all the values from the person
             * editor and sends them to the service call. 
             */  
            override protected function buildSaveRequest():Object{
                var pEditor:PersonEditor = PersonEditor(editor);
                var removeDigitPattern:RegExp = /\D*/g;
                
                var args:Object = new Object();
                args.method = "SavePerson";
                args.id = person.id;
                args.title = pEditor.title.text;
                args.firstname = pEditor.firstName.text;
                args.lastname = pEditor.lastName.text;
                args.email = pEditor.email.text;
                args.cell = pEditor.cellphone.text.replace(removeDigitPattern, "");
                // Reformat the date to one that php/mysql can understand
                args.birth = BaseModel.DATE_FORMATER.format(pEditor.birth.text);
                args.details = pEditor.details.text;
                return args;
            }
            
            override protected function buildDeleteRequest():Object{           
                var args:Object = new Object();
                args.method = "DeletePerson";
                args.id = person.id;
                return args;
            }
            
            
            override protected function createLinkDialog():LinkDialog{
                var dialog:LinkDialog;
                dialog = new LinkDialog();
                dialog.setMode(LinkDialog.TYPE_PLACE);
                dialog.modelId = person.id;
                
                return dialog;
            }
            
        ]]>
    </mx:Script>
</thecamaras:CommonEditor>
