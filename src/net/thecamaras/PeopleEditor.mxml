<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" icon="{PERSON_ICON}" label="New" xmlns:thecamaras="net.thecamaras.*">
    <mx:Style >        
        .personTitle {
            font-size:30;   
        }
    </mx:Style>

    <mx:Script >
        <![CDATA[
            import mx.messaging.messages.IMessage;
            import mx.rpc.events.FaultEvent;
            import mx.events.CloseEvent;
            import mx.utils.ObjectUtil;
            import net.thecamaras.models.Person;
            import mx.controls.Alert;
            
            
            [Embed(source="assets/person.png")]
            private static const PERSON_ICON:Class;
            
            [Bindable]
            private var person:Person;
            private var modelSet:Boolean = false;
            
            public function set model (xml:XML):void{
                if (!initialized){
                    this.content2.enabled = false;
                }
                this.label = "loading..";
                var args:Object = new Object();
                args.method = "Person";
                args.id = xml.@id;
                Connection.instance.send("address.php", args, personLoaded); 
            }
            
            private function personLoaded(xml:XML):void{
                this.person = new Person(xml.data.Person[0]);
                this.modelSet = true;
                this.label = person.firstname;
                this.titleLabel.text = getName();
                this.pEditor.person = this.person;
                this.content2.enabled = true;
            }
            
            private function getName():String {
                if (person != null){
                    return person.firstname + ' ' + person.lastname;
                }
                else{
                    return "New Person";
                }
            }
            
            private function save():void{
                if (pEditor.isInvalid()){
                    Alert.show("Unable to save: Invalid values in the detail fields", 
                               "Error Saving..."); 
                    return;
                }
                var args:Object = new Object();
                args.method = "SavePerson";
                args.id = person.id;
                args.title = pEditor.title.text;
                args.firstname = pEditor.firstName.text;
                args.lastname = pEditor.lastName.text;
                args.email = pEditor.email.text;
                args.cell = pEditor.cellphone.text;
                args.birth = Person.DATE_FORMATER.format(pEditor.birth.text);
                args.details = pEditor.details.text;
                Connection.instance.send2("address.php", args, saveComplete, saveFailed);
                this.content2.enabled = false;
            }
            
            private function saveComplete(result:XML):void{
                personLoaded(result);
                pEditor.dirty = false;
            }
            
            private function saveFailed(e:FaultEvent):void{
                var msg:IMessage = IMessage(e.message);
                Alert.show(msg.toString(), "Error saving");
            }
            
            private function close():void{
                if (pEditor.dirty){
                    Alert.show("You have unsaved changes. Do you wish to close?", "Close", 
                    Alert.OK | Alert.CANCEL, this, doClose, null, Alert.CANCEL);     
                }
                else{
                    doClose(new CloseEvent(CloseEvent.CLOSE, false, false, Alert.OK));
                }
            }
            
            private function doClose(e:CloseEvent):void{
                if (e.detail != Alert.OK){
                    return;
                }
                
                // Should probally fire an event requesting close
                this.parent.removeChild(this);
            }
            
        ]]>
    </mx:Script>
    
    
    <mx:Label id="titleLabel" 
            styleName="personTitle" 
            text="{getName()}" >
            
    </mx:Label>
    <mx:VDividedBox id="content2" 
        x="0" y="0" height="90%" width="100%">
        <mx:HDividedBox width="100%" height="60%">
            <mx:TitleWindow id="peopleBox" title="Name" width="50%" height="100%">
            </mx:TitleWindow>
            <thecamaras:PersonEditor id="pEditor" width="50%" height="100%" />
        </mx:HDividedBox>
        <mx:TextArea id="placeListing" text="test" width="100%" height="40%">
            
        </mx:TextArea>
    </mx:VDividedBox>
    <mx:HBox width="100%" horizontalAlign="right">
        <mx:Button id="saveBtn" label="save" click="save();" enabled="{pEditor.dirty}"/>
        <mx:Button id="closeBtn" label="close" click="close();"/>
    </mx:HBox>
</mx:VBox>
